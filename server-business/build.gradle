plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java-library'

}
apply plugin: 'idea'
idea {
    module {
        outputDir file('build/classes/main')
        testOutputDir file('build/classes/test')
    }
}

group 'com.undecided'
bootRun {
    enabled = false
}
bootJar {
    enabled = false
}
jar {
    enabled = true
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/libs-release' }

}

dependencies {
    implementation project(":common-precondition")
    implementation project(":common-exception")
    implementation project(":common-message")

    compileOnly group: 'org.jetbrains', name: 'annotations', version: '23.0.0'
    testImplementation project(":common-test")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation "org.springframework.boot:spring-boot-starter-web"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation group: 'org.seasar.doma.boot', name: 'doma-spring-boot-starter', version: '1.7.0'
    annotationProcessor 'org.seasar.doma:doma-processor:2.53.1'
    implementation group: 'de.huxhorn.sulky', name: 'de.huxhorn.sulky.ulid', version: '8.3.0'

}
tasks.register('copyDomaResources', Copy) {
    from processResources.destinationDir
    into compileJava.destinationDirectory
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

compileJava {
    options.compilerArgs = ['-Adoma.domain.converters=com.undecided.projectTemplate.shared.daoConverter.DomainConvertersProvider',
                            '-Xlint:unchecked',
                            '-Xlint:deprecation',
                            '-parameters'
    ]
    options.encoding = 'UTF-8'
    dependsOn copyDomaResources

}
compileTestJava {
    options.compilerArgs = ['-proc:none']
    options.encoding = 'UTF-8'

}

test {
    useJUnitPlatform()
}